plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hilt)
}

ext {
    // The packageVariant defines the bootstrap variant that will be included in the app APK.
    // This must be supported by com.termux.shared.termux.TermuxBootstrap.PackageVariant or app will
    // crash at startup.
    // Bootstrap of a different variant must not be manually installed by the user after app installation
    // by replacing $PREFIX since app code is dependant on the variant used to build the APK.
    // Currently supported values are: [ "apt-android-7" "apt-android-5" ]
    packageVariant = System.getenv("TERMUX_PACKAGE_VARIANT") ?: "apt-android-7" // Default: "apt-android-7"
}

android {
    namespace "com.termux"

    compileSdk libs.versions.compileSdk.get().toInteger()
    ndkVersion = System.getenv("JITPACK_NDK_VERSION") ?: project.properties.ndkVersion
    def appVersionName = System.getenv("TERMUX_APP_VERSION_NAME") ?: ""
    def apkVersionTag = System.getenv("TERMUX_APK_VERSION_TAG") ?: ""
    def splitAPKsForDebugBuilds = System.getenv("TERMUX_SPLIT_APKS_FOR_DEBUG_BUILDS") ?: "1"
    def splitAPKsForReleaseBuilds = System.getenv("TERMUX_SPLIT_APKS_FOR_RELEASE_BUILDS") ?: "0" // F-Droid does not support split APKs #1904

    defaultConfig {
        applicationId "com.termux.kotlin"
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode 35
        versionName "1.0.37"

        if (appVersionName) versionName = appVersionName
        validateVersionName(versionName)

        buildConfigField "String", "TERMUX_PACKAGE_VARIANT", "\"" + project.ext.packageVariant + "\"" // Used by TermuxApplication class

        manifestPlaceholders.TERMUX_PACKAGE_NAME = "com.termux.kotlin"
        manifestPlaceholders.TERMUX_APP_NAME = "Termux"
        manifestPlaceholders.TERMUX_API_APP_NAME = "Termux:API"
        manifestPlaceholders.TERMUX_BOOT_APP_NAME = "Termux:Boot"
        manifestPlaceholders.TERMUX_FLOAT_APP_NAME = "Termux:Float"
        manifestPlaceholders.TERMUX_STYLING_APP_NAME = "Termux:Styling"
        manifestPlaceholders.TERMUX_TASKER_APP_NAME = "Termux:Tasker"
        manifestPlaceholders.TERMUX_WIDGET_APP_NAME = "Termux:Widget"

        externalNativeBuild {
            ndkBuild {
                cFlags "-std=c11", "-Wall", "-Wextra", "-Werror", "-Os", "-fno-stack-protector", "-Wl,--gc-sections"
            }
        }
    }

    splits {
        abi {
            enable ((gradle.startParameter.taskNames.any { it.contains("Debug") } && splitAPKsForDebugBuilds == "1") ||
                (gradle.startParameter.taskNames.any { it.contains("Release") } && splitAPKsForReleaseBuilds == "1"))
            reset ()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    signingConfigs {
        debug {
            storeFile file('testkey_untrusted.jks')
            keyAlias 'alias'
            storePassword 'xrj45yWGLbsO7W0v'
            keyPassword 'xrj45yWGLbsO7W0v'
        }
        release {
            def signingPropsFile = rootProject.file('signing.properties')
            if (signingPropsFile.exists()) {
                def signingProps = new Properties()
                signingProps.load(new FileInputStream(signingPropsFile))
                storeFile file(signingProps['storeFile'])
                storePassword signingProps['storePassword']
                keyAlias signingProps['keyAlias']
                keyPassword signingProps['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // Reproducible builds
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }

    lint {
        disable 'ProtectedPermissions'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "debug") {
                def abi = output.getFilter(com.android.build.OutputFile.ABI)
                outputFileName = new File("termux-app_" + (apkVersionTag ? apkVersionTag : project.ext.packageVariant + "-" + "debug") + "_" + (abi ? abi : "universal") + ".apk")
            } else if (variant.buildType.name == "release") {
                def abi = output.getFilter(com.android.build.OutputFile.ABI)
                outputFileName = new File("termux-app_" + (apkVersionTag ? apkVersionTag : project.ext.packageVariant + "-" + "release") + "_" + (abi ? abi : "universal") + ".apk")
            }
        }
    }

    buildFeatures {
        buildConfig true
        compose true
    }
}

dependencies {
    // Project modules
    implementation project(":terminal-view")
    implementation project(":termux-shared")
    
    // Kotlin
    implementation libs.kotlin.stdlib
    implementation libs.bundles.coroutines
    implementation libs.kotlin.serialization.json
    
    // AndroidX Core
    implementation libs.androidx.annotation
    implementation libs.androidx.core.ktx
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.activity.compose
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.drawerlayout
    implementation libs.androidx.preference
    implementation libs.androidx.viewpager
    
    // Lifecycle
    implementation libs.bundles.lifecycle
    implementation libs.bundles.lifecycle.compose
    
    // DataStore
    implementation libs.androidx.datastore.preferences
    
    // Room Database
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    ksp libs.androidx.room.compiler
    
    // Compose
    implementation platform(libs.compose.bom)
    implementation libs.bundles.compose
    debugImplementation libs.bundles.compose.debug
    
    // Hilt DI
    implementation libs.hilt.android
    ksp libs.hilt.compiler
    implementation libs.hilt.navigation.compose
    
    // UI Libraries
    implementation libs.material
    implementation libs.bundles.markwon
    
    // Utilities
    implementation libs.guava
    implementation libs.guava.listenablefuture
    
    // Testing
    testImplementation libs.bundles.testing
    testImplementation libs.robolectric
    androidTestImplementation libs.bundles.testing.android
    androidTestImplementation platform(libs.compose.bom)
    androidTestImplementation libs.compose.ui.test.junit4
    
    // Desugaring
    coreLibraryDesugaring libs.desugar.jdk.libs
}

task versionName {
    doLast {
        print android.defaultConfig.versionName
    }
}

def validateVersionName(String versionName) {
    // https://semver.org/spec/v2.0.0.html#is-there-a-suggested-regular-expression-regex-to-check-a-semver-string
    // ^(0|[1-9]\d*)\.(0|[1-9]\d*)\.(0|[1-9]\d*)(?:-((?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\.(?:0|[1-9]\d*|\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?(?:\+([0-9a-zA-Z-]+(?:\.[0-9a-zA-Z-]+)*))?$
    if (!java.util.regex.Pattern.matches("^(0|[1-9]\\d*)\\.(0|[1-9]\\d*)\\.(0|[1-9]\\d*)(?:-((?:0|[1-9]\\d*|\\d*[a-zA-Z-][0-9a-zA-Z-]*)(?:\\.(?:0|[1-9]\\d*|\\d*[a-zA-Z-][0-9a-zA-Z-]*))*))?(?:\\+([0-9a-zA-Z-]+(?:\\.[0-9a-zA-Z-]+)*))?\$", versionName))
        throw new GradleException("The versionName '"  + versionName + "' is not a valid version as per semantic version '2.0.0' spec in the format 'major.minor.patch(-prerelease)(+buildmetadata)'. https://semver.org/spec/v2.0.0.html.")
}

def downloadBootstrap(String arch, String expectedChecksum, String version, File projDir) {
    def digest = java.security.MessageDigest.getInstance("SHA-256")

    def localUrl = "src/main/cpp/bootstrap-" + arch + ".zip"
    def file = new File(projDir, localUrl)
    if (file.exists()) {
        def buffer = new byte[8192]
        def input = new FileInputStream(file)
        while (true) {
            def readBytes = input.read(buffer)
            if (readBytes < 0) break
            digest.update(buffer, 0, readBytes)
        }
        def checksum = new BigInteger(1, digest.digest()).toString(16)
        while (checksum.length() < 64) { checksum = "0" + checksum }
        if (checksum == expectedChecksum) {
            return
        } else {
            println("Deleting old local file with wrong hash: " + localUrl + ": expected: " + expectedChecksum + ", actual: " + checksum)
            file.delete()
        }
    }

    // GitHub release tags use apt.android-N format (with dot), but code uses apt-android-N (with dash)
    // Convert apt-android-N to apt.android-N for the URL
    def urlVersion = version.replace("apt-android-", "apt.android-")
    def remoteUrl = "https://github.com/termux/termux-packages/releases/download/bootstrap-" + urlVersion + "/bootstrap-" + arch + ".zip"
    println("Downloading " + remoteUrl + " ...")

    file.parentFile.mkdirs()
    def out = new BufferedOutputStream(new FileOutputStream(file))

    def connection = new URL(remoteUrl).openConnection()
    connection.setInstanceFollowRedirects(true)
    def digestStream = new java.security.DigestInputStream(connection.inputStream, digest)
    out << digestStream
    out.close()

    def checksum = new BigInteger(1, digest.digest()).toString(16)
    while (checksum.length() < 64) { checksum = "0" + checksum }
    if (checksum != expectedChecksum) {
        file.delete()
        throw new GradleException("Wrong checksum for " + remoteUrl + ": expected: " + expectedChecksum + ", actual: " + checksum)
    }
}

clean {
    def cppDir = new File(projectDir, 'src/main/cpp')
    doLast {
        def tree = fileTree(cppDir)
        tree.include 'bootstrap-*.zip'
        tree.each { it.delete() }
    }
}

// Capture project references at configuration time for configuration cache compatibility
def bootstrapPackageVariant = project.ext.packageVariant
def bootstrapProjectDir = projectDir

task downloadBootstraps() {
    doLast {
        if (bootstrapPackageVariant == "apt-android-7") {
            def version = "2026.01.17-r3" + "+" + bootstrapPackageVariant
            // Custom bootstrap with native com.termux.kotlin paths for ALL architectures
            // Built apt/dpkg/termux-tools/termux-exec from source with TERMUX_APP__PACKAGE_NAME=com.termux.kotlin
            // All paths compiled natively into binaries - no runtime patching needed
            downloadBootstrap("aarch64", "feae9b6171f616963991ffa49582afeee4dcac12d9a823af6f2279c145f56e09", version, bootstrapProjectDir)
            downloadBootstrap("arm", "ab612d70d4ac3f84ffd3c8104fd98bb4b88709e3bbb625aa5dfae0ddaf6c68ab", version, bootstrapProjectDir)
            downloadBootstrap("i686", "f517e19b892eace931885c3fa91b4f52ba3fa6bfe5ba49bfbb581a1ce86d2736", version, bootstrapProjectDir)
            downloadBootstrap("x86_64", "1e0b08beb01b4f4b420e135901e2ef2a18c8490d586e204c275adef4f03bc756", version, bootstrapProjectDir)
        } else if (bootstrapPackageVariant == "apt-android-5") {
            // Note: apt-android-5 is no longer actively maintained by Termux.
            // Using legacy checksums for compatibility with Android 5/6 devices.
            def version = "2022.04.28-r6" + "+" + bootstrapPackageVariant
            downloadBootstrap("aarch64", "913609d439415c828c5640be1b0561467e539cb1c7080662decaaca2fb4820e7", version, bootstrapProjectDir)
            downloadBootstrap("arm", "26bfb45304c946170db69108e5eb6e3641aad751406ce106c80df80cad2eccf8", version, bootstrapProjectDir)
            downloadBootstrap("i686", "46dcfeb5eef67ba765498db9fe4c50dc4690805139aa0dd141a9d8ee0693cd27", version, bootstrapProjectDir)
            downloadBootstrap("x86_64", "615b590679ee6cd885b7fd2ff9473c845e920f9b422f790bb158c63fe42b8481", version, bootstrapProjectDir)
        } else {
            throw new GradleException("Unsupported TERMUX_PACKAGE_VARIANT \"" + bootstrapPackageVariant + "\"")
        }
    }
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.javaCompileProvider.get().dependsOn(downloadBootstraps)
    }
}

// Make preBuild depend on downloadBootstraps to ensure bootstrap files exist before any build
tasks.named('preBuild').configure {
    dependsOn downloadBootstraps
}
