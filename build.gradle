plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.compose) apply false
    alias(libs.plugins.ksp) apply false
    alias(libs.plugins.hilt) apply false
    alias(libs.plugins.detekt) apply false
}

// Keep these for backward compatibility with existing modules
buildscript {
    ext.kotlin_version = '2.0.21'
    ext.detekt_version = '1.23.7'
    ext.markwonVersion = '4.6.2'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

// Apply Detekt to all subprojects
subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'
    
    detekt {
        buildUponDefaultConfig = true
        allRules = false
        config.setFrom(files("$rootDir/config/detekt/detekt.yml"))
        baseline = file("$projectDir/config/detekt/baseline.xml")
        parallel = true
    }
    
    tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
        jvmTarget = "17"
        reports {
            html.required.set(true)
            xml.required.set(true)
            txt.required.set(false)
            sarif.required.set(true)
        }
    }
}
